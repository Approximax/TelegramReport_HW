
plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group = "org.example"
version = "1.0-SNAPSHOT"

allure {
    report {
        version.set("2.25.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.25.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

compileJava {
    options.encoding = 'UTF-8'
}
compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.10.1',
            'com.codeborne:selenide:6.19.1',
            'io.qameta.allure:allure-selenide:2.25.0',
            'org.aeonbits.owner:owner:1.0.12'
    )
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task search_test(type: Test) {
    useJUnitPlatform {
        includeTags("search")
    }
}

task login_test(type: Test) {
    useJUnitPlatform {
        includeTags("login")
    }
}